plugins {
    id "java"
    id "com.qixalite.spongestart" version "1.6.2"
    id "ninja.miserable.blossom" version "1.0.1"
    id "com.github.johnrengelman.shadow" version "2.0.1"
}

group = "valandur.webapi";

allprojects {
    apply plugin: "java"

    version = "${project.version}-S${project.spongeVersion}";

    repositories {
        mavenCentral()
        maven {
            url "https://repo.spongepowered.org/maven/"
        }
        maven {
            url "http://repo.bstats.org/content/repositories/releases/"
        }
    }

    dependencies {
        compileOnly group: "org.spongepowered", name: "spongeapi", version: "${project.spongeVersion}.0-SNAPSHOT"
    }
}

blossom {
    def locMain = "src/main/java/valandur/webapi/WebAPI.java"
    replaceToken "@version@", project.version, locMain
}

spongestart {
    eula true
    minecraft project.minecraftVersion
}

dependencies {
    compile project(":webapi-api")
    compile group: "io.sentry", name: "sentry", version: "1.5.3"
    compile group: "org.mindrot", name: "jbcrypt", version: "0.4"
    compile group: "org.bstats", name: "bstats-sponge", version: "1.2"

    compileOnly group: "com.fasterxml.jackson.core", name: "jackson-core", version: "2.9.0"
    compileOnly group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.9.0"
    compileOnly group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: "2.9.0"
    compileOnly group: "com.fasterxml.jackson.dataformat", name: "jackson-dataformat-xml", version: "2.9.0"
    compileOnly group: "org.codehaus.woodstox", name: "woodstox-core-asl", version: "4.4.1"
    compileOnly group: "org.eclipse.jetty", name: "jetty-server", version: "9.4.7.v20170914"
    compileOnly group: "org.eclipse.jetty", name: "jetty-servlet", version: "9.4.7.v20170914"
    compileOnly group: "org.eclipse.jetty", name: "jetty-http", version: "9.4.7.v20170914"
    compileOnly group: "org.eclipse.jetty", name: "jetty-rewrite", version: "9.4.7.v20170914"

    compileOnly files("lib/HuskyCrates-v1.7.2.jar")
    compileOnly files("lib/MMCTickets-1.4.0-API-5.2-7.X.jar")
    compileOnly files("lib/Nations-2.8-S6.1-MC1.11.2.jar")
    compileOnly files("lib/Nucleus-1.1.3-LTS-api.jar")
    compileOnly files("lib/RedProtect-Sponge-6.1.0-7.2.0-b137.jar")
}

shadowJar {
    configurations  = [project.configurations.compile]

    duplicatesStrategy = DuplicatesStrategy.FAIL

    relocate "edu.umd", "valandur.webapi.shadow.edu.umd"
    relocate "io.sentry", "valandur.webapi.shadow.io.sentry"
    relocate "javassist", "valandur.webapi.shadow.javassist"
    relocate "net.jodah", "valandur.webapi.shadow.net.jodah"
    relocate "net.jcip", "valandur.webapi.shadow.net.jcip"
    relocate "org.bstats", "valandur.webapi.shadow.org.bstats"
    relocate "org.mindrot", "valandur.webapi.shadow.org.mindrot"

    exclude "/about.html"
    exclude "/jetty-dir.css"
    exclude "/valandur/webapi/api/WebAPIAPIMod.class"

    relocate "com.ctc", "valandur.webapi.shadow.com.ctc"
    relocate "com.fasterxml", "valandur.webapi.shadow.com.fasterxml"
    relocate "javax.servlet", "valandur.webapi.shadow.javax.servlet"
    relocate "javax.xml", "valandur.webapi.shadow.javax.xml"
    relocate "org.codehaus", "valandur.webapi.shadow.org.codehaus"
    relocate "org.eclipse", "valandur.webapi.shadow.org.eclipse"

    dependencies {
        exclude(dependency("com.fasterxml.*:"))
        exclude(dependency("com.ctc.*:"))
        exclude(dependency("javax.servlet:"))
        exclude(dependency("javax.xml.*:"))
        exclude(dependency("org.codehaus.*:"))
        exclude(dependency("org.eclipse.jetty:"))
    }

    archiveName = "webapi-${version}.jar"
}
shadowJar.dependsOn([':webapi-api:build'])

task copyJars(type: Copy, dependsOn: ":webapi-api:build") {
    from([subprojects.jar, shadowJar])
    into project.file("artifacts")
}

artifacts {
    archives shadowJar
}

build.dependsOn(shadowJar)
build.dependsOn(copyJars)
