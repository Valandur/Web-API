plugins {
    id "java"
    id "com.qixalite.spongestart" version "1.6.2"
    id "ninja.miserable.blossom" version "1.0.1"
    id "com.github.johnrengelman.shadow" version "2.0.1"
}

group = "valandur.webapi";

allprojects {
    apply plugin: "java"

    version = "${project.version}-S${project.spongeVersion}";

    repositories {
        mavenCentral()
        maven {
            url "https://repo.spongepowered.org/maven/"
        }
    }

    dependencies {
        compileOnly group: "org.spongepowered", name: "spongeapi", version: "${project.spongeVersion}.0-SNAPSHOT"
    }
}

blossom {
    def locMain = "src/main/java/valandur/webapi/WebAPI.java"
    replaceToken "@version@", project.version, locMain
}

spongestart {
    eula true
    minecraft project.minecraftVersion
    spongeForgeVersion "1.12.2-2555-7.1.0-BETA-2831"
    spongeVanillaVersion "1.12.2-7.1.0-BETA-9"
}

repositories {
    maven {
        url "http://repo.bstats.org/content/repositories/releases/"
    }
    maven {
        url = "http://repo.aikar.co/nexus/content/groups/aikar/"
    }
    flatDir {
        dirs "lib/7"
    }
}

dependencies {
    compile project(":webapi-api")
    compile group: "io.sentry", name: "sentry", version: "1.6.4"
    compile group: "org.mindrot", name: "jbcrypt", version: "0.4"
    compile group: "org.bstats", name: "bstats-sponge", version: "1.2"
    compile group: "co.aikar", name: "minecraft-timings", version: "1.0.4"
    compile group: "com.github.oshi", name: "oshi-core", version: "3.4.4"

    compileOnly group: "com.fasterxml.jackson.core", name: "jackson-core", version: project.jacksonVersion
    compileOnly group: "com.fasterxml.jackson.core", name: "jackson-databind", version: project.jacksonVersion
    compileOnly group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: project.jacksonVersion
    compileOnly group: "com.fasterxml.jackson.dataformat", name: "jackson-dataformat-xml", version: project.jacksonVersion
    compileOnly group: "org.codehaus.woodstox", name: "woodstox-core-asl", version: "4.4.1"
    compileOnly group: "org.eclipse.jetty", name: "jetty-server", version: project.jettyVersion
    compileOnly group: "org.eclipse.jetty", name: "jetty-servlet", version: project.jettyVersion
    compileOnly group: "org.eclipse.jetty", name: "jetty-http", version: project.jettyVersion
    compileOnly group: "org.eclipse.jetty", name: "jetty-rewrite", version: project.jettyVersion

    compileOnly name: "griefprevention-1.12.2-4.3.0.511"
    compileOnly name: "HuskyCrates-v1.8.0PRE2-API7"
    compileOnly name: "MMCTickets-1.4.1-API-7"
    compileOnly name: "Nucleus-1.2.2-S7.0-api"
    compileOnly name: "UniversalMarket-1.12.2-v1.1"
    compileOnly name: "WebBooks"
}

shadowJar {
    configurations  = [project.configurations.compile]

    duplicatesStrategy = DuplicatesStrategy.FAIL

    mergeServiceFiles()

    //relocate "com.sun.jna", "valandur.webapi.shadow.com.sun.jna"
    relocate "co.aikar.timings.lib", "valandur.webapi.shadow.co.aikar.timingslib"
    relocate "edu.umd", "valandur.webapi.shadow.edu.umd"
    relocate "io.sentry", "valandur.webapi.shadow.io.sentry"
    relocate "javassist", "valandur.webapi.shadow.javassist"
    relocate "net.jodah", "valandur.webapi.shadow.net.jodah"
    relocate "net.jcip", "valandur.webapi.shadow.net.jcip"
    relocate "org.bstats", "valandur.webapi.shadow.org.bstats"
    relocate "org.mindrot", "valandur.webapi.shadow.org.mindrot"
    relocate "org.intellij", "valandur.webapi.shadow.org.intellij"
    relocate "org.jetbrains", "valandur.webapi.shadow.org.jetbrains"
    relocate "org.threeten", "valandur.webapi.shadow.org.threeten"
    relocate "oshi", "valandur.webapi.shadow.oshi"

    exclude "/about.html"
    exclude "/jetty-dir.css"

    // We need to relocate these dependencies, even though they're excluded because
    // we need the references in our code to be changed to the shadowed dependencies
    // These dependencies are already included by WebAPI-API
    relocate "com.ctc", "valandur.webapi.shadow.com.ctc"
    relocate "com.fasterxml", "valandur.webapi.shadow.com.fasterxml"
    relocate "javax.servlet", "valandur.webapi.shadow.javax.servlet"
    relocate "javax.xml", "valandur.webapi.shadow.javax.xml"
    relocate "org.codehaus", "valandur.webapi.shadow.org.codehaus"
    relocate "org.eclipse", "valandur.webapi.shadow.org.eclipse"

    // Exclude the dependencies already included by WebAPI-API
    dependencies {
        exclude(dependency("com.ctc.*:"))
        exclude(dependency("com.fasterxml.*:"))
        exclude(dependency("javax.servlet.*:"))
        exclude(dependency("javax.xml.*:"))
        exclude(dependency("org.codehaus.*:"))
        exclude(dependency("org.eclipse.*:"))

        // Provided by sponge/forge
        exclude(dependency("org.slf4j.*:"))
    }

    archiveName = "webapi-${version}.jar"
}
shadowJar.dependsOn([':webapi-api:build'])

task copyJars(type: Copy, dependsOn: ":webapi-api:build") {
    from([subprojects.jar, shadowJar])
    into project.file("artifacts")
}

artifacts {
    archives shadowJar
}

build.dependsOn(shadowJar)
build.dependsOn(copyJars)
