plugins {
    id "java"
    id "com.qixalite.spongestart" version "1.6.2"
    id "ninja.miserable.blossom" version "1.0.1"
    id "com.github.johnrengelman.shadow" version "2.0.1"
}

group = "valandur.webapi"

apply plugin: "java"

version = "${project.version}-S${project.spongeVersion}"

blossom {
    def locMain = "src/main/java/valandur/webapi/util/Constants.java"
    replaceToken "@version@", project.version, locMain
}

spongestart {
    eula true
    minecraft project.minecraftVersion
    spongeForgeVersion "1.12.2-2705-7.1.0-BETA-3206"
    spongeVanillaVersion "1.12.2-7.1.0-BETA-59"
}

repositories {
    mavenCentral()
    maven {
        url "https://repo.spongepowered.org/maven/"
    }
    maven {
        url "http://repo.bstats.org/content/repositories/releases/"
    }
    maven {
        url = "http://repo.aikar.co/nexus/content/groups/aikar/"
    }
    flatDir {
        dirs "lib"
    }
}

configurations.all {
    // Exclude because included by Sponge/Minecraft
    exclude group: "org.apache.commons"
    exclude group: "com.google.common"

    resolutionStrategy {
        force "com.google.guava:guava:23.0"
        force "org.reflections:reflections:0.9.10"      // Fixes some logging issues in 0.9.11
    }
}

dependencies {
    compile group: "co.aikar", name: "minecraft-timings", version: "1.0.4"

    compile group: "com.fasterxml.jackson.core", name: "jackson-core", version: project.jacksonVersion
    compile group: "com.fasterxml.jackson.core", name: "jackson-databind", version: project.jacksonVersion
    compile group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: project.jacksonVersion
    compile group: "com.fasterxml.jackson.dataformat", name: "jackson-dataformat-xml", version: project.jacksonVersion
    compile group: "com.fasterxml.jackson.jaxrs", name: "jackson-jaxrs-json-provider", version: project.jacksonVersion
    // compile group: "io.swagger.core.v3", name: "swagger-jaxrs2", version: "2.0.0-rc3"    // This if for Swagger 3.0

    compile group: "org.codehaus.woodstox", name: "woodstox-core-asl", version: "4.4.1"

    compile group: "org.eclipse.jetty", name: "jetty-server", version: project.jettyVersion
    compile group: "org.eclipse.jetty", name: "jetty-servlet", version: project.jettyVersion
    compile group: "org.eclipse.jetty", name: "jetty-rewrite", version: project.jettyVersion
    compile group: "org.eclipse.jetty.websocket", name: "websocket-server", version: project.jettyVersion
    compile group: "org.eclipse.jetty.websocket", name: "websocket-servlet", version: project.jettyVersion
    compile group: "org.eclipse.jetty.websocket", name: "websocket-client", version: project.jettyVersion

    compile group: "io.swagger", name: "swagger-jersey2-jaxrs", version: "1.5.18"
    compile group: "io.sentry", name: "sentry", version: "1.6.4"

    compile group: "org.mindrot", name: "jbcrypt", version: "0.4"
    compile group: "org.glassfish.jersey.containers", name: "jersey-container-servlet", version: "2.26"
    compile group: "org.glassfish.jersey.inject", name: "jersey-hk2", version: "2.26"
    compile group: "org.bstats", name: "bstats-sponge", version: "1.2"

    compileOnly group: "org.spongepowered", name: "spongeapi", version: "${project.spongeVersion}.0-SNAPSHOT"

    // Integrations
    compileOnly name: "ActiveTime-s7.1-v1.4.0"
    compileOnly name: "CmdScheduler-s7.1-v1.1.0"
    compileOnly name: "HuskyCrates-v1.8.0PRE2-API7"
    compileOnly name: "MMCRestrict-1.4.2-API-7"
    compileOnly name: "MMCTickets-1.4.1-API-7"
    compileOnly name: "Nucleus-1.3.3-S7.0-api"
    compileOnly name: "RedProtect-7.3.0-b68-Universal"
    compileOnly name: "UniversalMarket-1.12.2-v1.1"
    compileOnly name: "WebBooks"
}

shadowJar {
    configurations  = [project.configurations.compile]

    mergeServiceFiles()

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    relocate "co.aikar.timings.lib", "valandur.webapi.shadow.co.aikar.timings.lib"

    relocate "com.ctc", "valandur.webapi.shadow.com.ctc"
    relocate "com.fasterxml", "valandur.webapi.shadow.com.fasterxml"

    relocate "com.google.errorprone", "valandur.webapi.shadow.com.google.errorprone"
    relocate "com.google.j2objc", "valandur.webapi.shadow.com.google.j2objc"
    relocate "com.google.thirdparty", "valandur.webapi.shadow.com.google.thirdparty"

    relocate "com.sun.research", "valandur.webapi.shadow.com.sun.research"

    relocate "edu.umd.cs.findbugs.annotations", "valandur.webapi.shadow.edu.umd.cs.findbugs.annotations"

    relocate "io.sentry", "valandur.webapi.shadow.io.sentry"
    relocate "io.swagger", "valandur.webapi.shadow.io.swagger"

    relocate "javassist", "valandur.webapi.shadow.javassist"

    relocate "javax.annotation", "valandur.webapi.shadow.javax.annotation"
    relocate "javax.inject", "valandur.webapi.shadow.javax.inject"
    relocate "javax.servlet", "valandur.webapi.shadow.javax.servlet"
    relocate "javax.validation", "valandur.webapi.shadow.javax.validation"
    relocate "javax.ws", "valandur.webapi.shadow.javax.ws"
    relocate "javax.xml.stream", "valandur.webapi.shadow.javax.xml.stream"

    relocate "jersey.repackaged", "valandur.webapi.shadow.jersey.repackaged"

    relocate "net.jcip.annotations", "valandur.webapi.shadow.net.jcip.annotations"

    relocate "org.aopalliance", "valandur.webapi.shadow.org.aopalliance"
    relocate "org.bstats", "valandur.webapi.shadow.org.bstats"
    relocate "org.codehaus", "valandur.webapi.shadow.org.codehaus"
    relocate "org.threeten", "valandur.webapi.shadow.org.threeten"

    relocate "org.eclipse.jetty.http", "valandur.webapi.shadow.org.eclipse.jetty.http"
    relocate "org.eclipse.jetty.io", "valandur.webapi.shadow.org.eclipse.jetty.io"
    relocate "org.eclipse.jetty.rewrite", "valandur.webapi.shadow.org.eclipse.jetty.rewrite"
    relocate "org.eclipse.jetty.security", "valandur.webapi.shadow.org.eclipse.jetty.security"
    relocate "org.eclipse.jetty.server", "valandur.webapi.shadow.org.eclipse.jetty.server"
    relocate "org.eclipse.jetty.servlet", "valandur.webapi.shadow.org.eclipse.jetty.servlet"
    relocate "org.eclipse.jetty.util", "valandur.webapi.shadow.org.eclipse.jetty.util"
    relocate "org.eclipse.jetty.version", "valandur.webapi.shadow.org.eclipse.jetty.version"

    relocate "org.glassfish", "valandur.webapi.shadow.org.glassfish"
    relocate "org.intellij", "valandur.webapi.shadow.org.intellij"
    relocate "org.jetbrains", "valandur.webapi.shadow.org.jetbrains"
    relocate "org.jvnet", "valandur.webapi.shadow.org.jvnet"
    relocate "org.mindrot", "valandur.webapi.shadow.org.mindrot"
    relocate "org.reflections", "valandur.webapi.shadow.org.reflections"

    relocate "org.slf4j.event", "valandur.webapi.shadow.org.slf4j.event"
    relocate "org.slf4j.helpers", "valandur.webapi.shadow.org.slf4j.helpers"
    relocate "org.slf4j.spi", "valandur.webapi.shadow.org.slf4j.spi"

    relocate "org.yaml", "valandur.webapi.shadow.org.yaml"

    // Provided by Sponge/Minecraft
    dependencies {
        exclude(dependency("org.apache.commons.*:"))
        exclude(dependency("com.google.guava.*:"))
        exclude(dependency("org.slf4j.*:"))
    }

    exclude "/about.html"
    exclude "/jetty-dir.css"
    exclude "/org/eclipse/jetty/favicon.ico"

    archiveName = "webapi-${version}.jar"
}

task copyJars(type: Copy) {
    from([shadowJar])
    into project.file("artifacts")
}
copyJars.dependsOn(shadowJar)

artifacts {
    archives shadowJar
}

build.dependsOn(shadowJar)
build.dependsOn(copyJars)
