plugins {
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

group = 'io.valandur'
version = '1.0-SNAPSHOT'

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'
    apply plugin: 'maven'

    group = project.group
    version = project.version

    sourceCompatibility = '11'
    targetCompatibility = '11'

    repositories {
        mavenCentral()
    }

    jar {
        enabled = false
        dependsOn(shadowJar)
    }

    configurations {
        implementation.extendsFrom include
    }

    shadowJar {
        configurations = [project.configurations.include]

        archiveClassifier.set('')

        mergeServiceFiles()

        relocate 'com.fasterxml', 'io.valandur.shadow.com.fasterxml'
        relocate 'com.sun.research', 'io.valandur.shadow.com.sun.research'

        // If we enable this then we have to fix strings in our code
        // relocate 'graphql', 'io.valandur.shadow.graphql'

        relocate 'io.github', 'io.valandur.shadow.io.github'
        relocate 'io.leangen', 'io.valandur.shadow.io.leangen'

        relocate 'jakarta.annotation', 'io.valandur.shadow.jakarta.annotation'
        relocate 'jakarta.inject', 'io.valandur.shadow.jakarta.inject'
        relocate 'jakarta.servlet', 'io.valandur.shadow.jakarta.servlet'
        relocate 'jakarta.validation', 'io.valandur.shadow.jakarta.validation'
        relocate 'jakarta.ws', 'io.valandur.shadow.jakarta.ws'

        relocate 'javassist', 'io.valandur.shadow.javassist'

        relocate 'javax.activation', 'io.valandur.shadow.javax.activation'
        relocate 'javax.servlet', 'io.valandur.shadow.javax.servlet'
        // If we enable this then Sponge (and others?) just break
        // relocate 'javax.xml', 'io.valandur.shadow.javax.xml'

        relocate 'jersey.repackaged', 'io.valandur.shadow.jersey.repackaged'

        relocate 'net.dongliu', 'io.valandur.shadow.net.dongliu'

        relocate 'nonapi.io', 'io.valandur.shadow.nonapi.io'

        relocate 'org.antlr', 'io.valandur.shadow.org.antlr'
        relocate 'org.aopalliance', 'io.valandur.shadow.org.aopalliance'
        relocate 'org.dataloader', 'io.valandur.shadow.org.dataloader'
        relocate 'org.eclipse', 'io.valandur.shadow.org.eclipse'
        relocate 'org.glassfish', 'io.valandur.shadow.org.glassfish'
        relocate 'org.jvnet', 'io.valandur.shadow.org.jvnet'
        relocate 'org.reactivestreams', 'io.valandur.shadow.org.reactivestreams'
        relocate 'org.slf4j', 'io.valandur.shadow.org.slf4j'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
